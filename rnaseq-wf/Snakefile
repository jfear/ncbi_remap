"""RNA-Seq Alignment workflow.

The goal of the RNA-Seq workflow is to use parameters determined in the
pre-alignment workflow and processing files. This workflow uses a queue in
`../output/sra.h5`. To update the queue you need to run `./aln-store.py queue update
-j 8`. The major file types output by the alignment workflow include:

* Strand specific BigWig tracks
* Gene level coverage counts and junction counts
* Intergenic coverage counts and junction counts

Rules
-----
targets
    create a list of desired output files
intergenic
    create an intergenic bed and gtf based on the flybase gtf
fastq_dump
    download fastq file from SRA, determine if the file is pair-end or
    single-end and count the number of reads and average read length
atropos
    trim illumina adapters and low quality bases (<20) from fastq file, remove
    reads that have fewer than 25 bp
hisat2_splice_site
    create set of splice sites using the flybase gtf
hisat2
    align reads to the flybase reference
hisat2_summary
    parse alignment log and determine if the alignment failed (<50% aligned)
expMerge
    merge bams (SRRs) to the library (SRX) level
feature_counts
    count the number of reads that overlap genic regions and junction counts
featurecounts_intergenic
    count the number of reads that overlap intergenic regions and junction
    counts
run_stats
    calculate basic stats with `samtools stats`, `samtools idxstats`, and
    `bamtools stats`
bamCoverage
    create strand specific coverage tracks (bedgraph) using deeptools
convertToFlybase
    convert bedgraph chromosome names (UCSC format) to flybase chromosome names
convertBedGraphToBigWig
    convert begraph to bigwig
"""

import os
import sys
from pathlib import Path
import re
import shutil

import numpy as np
import pandas as pd

sys.path.insert(0, "../src")
from ncbi_remap.queue import Queue
from ncbi_remap.snakemake import slack


# Setup tempdir to work with lscratch
if os.getenv("SLURM_JOBID"):
    TMPDIR = os.path.join('/lscratch', os.getenv('SLURM_JOBID'))
else:
    TMPDIR = os.getenv('TMPDIR', "/tmp")
shell.prefix("set -euo pipefail; export TMPDIR={};".format(TMPDIR))

singularity: "../singularity/drosSRA_workflow.sif"
configfile: '../config/reference_config.yaml'
onsuccess:
    print('All Finished')
    slack('rnaseq-wf: All Finished')
onerror:
    print('Something went wrong, you need to re-run')
    slack('rnaseq-wf: Something went wrong, you need to re-run') 

localrules: srx_complete

###############################################################################
# Set up file naming patterns and targets
###############################################################################
queue = Queue(
    targets="../output/prealn-wf/done.txt",
    subset="../output/library_strategy-wf/rnaseq_inliers.pkl",
    completed="../output/rnaseq-wf/done.txt",
    problems=[
        "../output/fastq-wf/download_bad",
        "../output/fastq-wf/abi_solid",
        "../output/prealn-wf/atropos_bad",
        "../output/prealn-wf/alignment_bad",
        "../output/rnaseq-wf/atropos_bad",
        "../output/rnaseq-wf/alignment_bad",
        "../output/rnaseq-wf/bigwig_bad",
    ],
    srx2srr="../output/srx2srr.csv",
    size=500
)

# If Dry-Run print queue
if any([x == "-n" for x in sys.argv]):
    print(queue)

rule run_all:
    input: expand("../output/rnaseq-wf/done/{srx}", srx=queue.srxs)

## Rules
include: "../fastq-wf/rules/download_fastq.smk"
include: "rules/atropos.smk"
include: "rules/align.smk"
include: "rules/merge.smk"
include: "rules/featurecounts.smk"
include: "rules/bedgraph.smk"
include: "rules/bigwig.smk"


###############################################################################
# What to keep
###############################################################################

rule srx_complete:
    input:
        lambda wildcards: queue.expand(rules.atropos_check.output[0], wildcards.srx),
        lambda wildcards: queue.expand(rules.hisat2_check.output[0], wildcards.srx),
        rules.bamMerge.output.bam,
        rules.aln_stats_summary.output[0],
        rules.parse_gene_count.output[0],
        rules.parse_junction_count.output[0],
        rules.parse_intergenic_count.output[0],
        rules.parse_segment_count.output[0],
        rules.parse_fusion_count.output[0],
        rules.bigwig_first.output[0],
        rules.bigwig_second.output[0],
        rules.flybase_bigwig_first.output[0],
        rules.flybase_bigwig_second.output[0],
    output: "../output/rnaseq-wf/done/{srx}"
    params:
        sample_id=lambda wildcards: wildcards.srx,
        sample_folder="../output/rnaseq-wf/samples/{srx}",
        done_queue="../output/rnaseq-wf/done.txt",
        srrs=lambda wildcards: queue.get_srrs(wildcards.srx),
    script: "../scripts/done_queue.py"
