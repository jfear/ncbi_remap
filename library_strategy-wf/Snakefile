""" Library Strategy processing workflow

`Library Strategy` is the description of what kind of sequencing was done
(i.e., RNA-Seq, ChIP-Seq, WGS). The SRA uses higher level terms that may not
be the most accurate description of a sample. I would like to provide more
detailed descriptions of the sample if they are availabled in the free text
metadata. I also want to validate the high level metadata, to ensure that
submissions with a mixture of strategies (e.g., RNA-Seq and ChIP-Seq) did not
swap any sample labels.

I will focus on RNA-Seq samples for downstream analysis, so it is most
important that these samples are correctly identified. I will use a data
driven machine learning approach. I will use various features from the
`prealn-wf` with a cross validation approach to confidently identify samples
that behave as expected.

"""
singularity: "../singularity/drosSRA_workflow.sif"

rule targets:
    input:
        "../output/library_strategy-wf/rnaseq_outliers.parquet",
        "../output/library_strategy-wf/rnaseq.pkl",
        # "../output/library_strategy-wf/pca_prealn_features_embeddings.parquet",
        # "../output/library_strategy-wf/library_strategy_outlier_shap_metrics.parquet",
        # Figures
        # "../output/library_strategy-wf/library_strategy.svg",
        # "../output/library_strategy-wf/library_strategy_outliers.svg",

################################################################################
# Prep Features
################################################################################
rule build_prealn_feature_set:
    input: "../output/srx2srr.csv"
    params:
        libsize="../output/fastq-wf/libsize",
        fastq_screen="../output/prealn-wf/fastq_screen",
        atropos="../output/prealn-wf/atropos",
        hisat2="../output/prealn-wf/hisat2",
        aln_stats="../output/prealn-wf/aln_stats",
        rnaseqmetrics="../output/prealn-wf/rnaseqmetrics",
        genebody_coverage="../output/prealn-wf/genebody_coverage",
        markduplicates="../output/prealn-wf/markduplicates",
        count_summary="../output/prealn-wf/count_summary",
        done="../output/prealn-wf/done"
    threads: 8
    output: "../output/library_strategy-wf/prealn_feature_set.parquet"
    script: "scripts/build_prealn_feature_set.py"


rule scale_feature_set:
    input: rules.build_prealn_feature_set.output[0]
    output: "../output/library_strategy-wf/scaled_prealn_feature_set.parquet"
    script: "scripts/scale_prealn_features.py"


rule pca_prealn_feature_set:
    input: rules.scale_feature_set.output[0]
    output: 
        embeddings="../output/library_strategy-wf/pca_prealn_features_embeddings.parquet",
        loadings="../output/library_strategy-wf/pca_prealn_features_loadings.parquet",
        variance="../output/library_strategy-wf/pca_prealn_features_explained_variance.parquet",
    script: "scripts/pca_prealn_feature_set.py"


rule umap_prealn_feature_set:
    input: rules.scale_feature_set.output[0]
    output: 
        embeddings="../output/library_strategy-wf/umap_prealn_features_embeddings.parquet",
        model="../output/library_strategy-wf/umap_prealn_features_model.joblib.pkl"
    script: "scripts/umap_prealn_feature_set.py"


rule pull_sra_strategy_and_selection_from_mongo:
    output:
        data="../output/library_strategy-wf/sra_strategy_selection.parquet",
        summary="../output/library_strategy-wf/sra_strategy_selection_summary_table.tsv",
    script: "scripts/pull_sra_strategy_and_selection_from_mongo.py"


################################################################################
# Find RNA-Seq Outliers
################################################################################
rule outlier_detection:
    input:
        features=rules.scale_feature_set.output[0],
        labels=rules.pull_sra_strategy_and_selection_from_mongo.output.data
    output: 
        outliers="../output/library_strategy-wf/rnaseq_outliers.parquet",
        rnaseq_samples="../output/library_strategy-wf/rnaseq.pkl",
        shap_model="../output/library_strategy-wf/shap_model.joblib.pkl",
        shap_values="../output/library_strategy-wf/shap_values.joblib.pkl",
        mixin="../output/library_strategy-wf/contamination_mixin.parquet",
    script: "scripts/outlier_detection.py"


################################################################################
# Refine Library Strategy and Selection Metadata
################################################################################
rule free_text_library_strategy:
    output: "../output/library_strategy-wf/free_text_library_strategy.parquet"
    script: "scripts/free_text_library_strategy.py"

# TODO: Re-think aggregation because I removed RF.
rule metadata_integration:
    input:
        sra=rules.pull_sra_strategy_and_selection_from_mongo.output.data,
        free_text=rules.free_text_library_strategy.output[0],
        # forest=rules.random_forest.output["predicted_labels"],
    output: "../output/library_strategy-wf/summarized_metadata.parquet"
    script: "scripts/metadata_integration.py"
