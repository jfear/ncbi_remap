""" Library Strategy processing workflow

`Library Strategy` is the description of what kind of sequencing was done
(i.e., RNA-Seq, ChIP-Seq, WGS). The SRA uses higher level terms that may not
be the most accurate description of a sample. I would like to provide more
detailed descriptions of the sample if they are availabled in the free text
metadata. I also want to validate the high level metadata, to ensure that
submissions with a mixture of strategies (e.g., RNA-Seq and ChIP-Seq) did not
swap any sample labels.

I will focus on RNA-Seq samples for downstream analysis, so it is most
important that these samples are correctly identified. I will use a data
driven machine learning approach. I will use various features from the
`prealn-wf` with a cross validation approach to confidently identify samples
that behave as expected.

"""
singularity: "../singularity/drosSRA_workflow.sif"

rule targets:
    input:
        "../output/library_strategy-wf/sra_strategy_selection.parquet",
        "../output/library_strategy-wf/pca_prealn_features_embeddings.parquet",
        "../output/library_strategy-wf/umap_prealn_features.parquet",
        "../output/library_strategy-wf/rnaseq_inliers.pkl",


rule pull_sra_strategy_and_selection_from_mongo:
    output:
        data="../output/library_strategy-wf/sra_strategy_selection.parquet",
        summary="../output/library_strategy-wf/sra_strategy_selection_summary_table.tsv",
    script: "scripts/pull_sra_strategy_and_selection_from_mongo.py"


################################################################################
# Prep Features
################################################################################
rule build_prealn_features:
    input: "../output/srx2srr.csv"
    params:
        libsize="../output/fastq-wf/libsize",
        fastq_screen="../output/prealn-wf/fastq_screen",
        atropos="../output/prealn-wf/atropos",
        hisat2="../output/prealn-wf/hisat2",
        aln_stats="../output/prealn-wf/aln_stats",
        rnaseqmetrics="../output/prealn-wf/rnaseqmetrics",
        genebody_coverage="../output/prealn-wf/genebody_coverage",
        markduplicates="../output/prealn-wf/markduplicates",
        count_summary="../output/prealn-wf/count_summary",
        done="../output/prealn-wf/done"
    threads: 8
    output: "../output/library_strategy-wf/prealn_features.parquet",
    script: "scripts/build_prealn_features.py"


rule scale_prealn_features:
    input: rules.build_prealn_features.output[0]
    output: "../output/library_strategy-wf/scaled_prealn_features.parquet"
    script: "scripts/scale_prealn_features.py"


rule pca_prealn_features:
    input: rules.scale_prealn_features.output[0]
    output: 
        embeddings="../output/library_strategy-wf/pca_prealn_features_embeddings.parquet",
        loadings="../output/library_strategy-wf/pca_prealn_features_loadings.parquet",
        variance="../output/library_strategy-wf/pca_prealn_features_explained_variance.parquet",
    script: "scripts/pca_prealn_features.py"


rule umap_prealn_features:
    input: rules.scale_prealn_features.output[0]
    output: "../output/library_strategy-wf/umap_prealn_features.parquet",
    script: "scripts/umap_prealn_features.py"


################################################################################
# Find RNA-Seq Outliers
################################################################################
rule split_features:
    input:
        features=rules.build_prealn_features.output[0],
        labels=rules.pull_sra_strategy_and_selection_from_mongo.output.data
    output: 
        rnaseq="../output/library_strategy-wf/rnaseq_features.parquet",
        est="../output/library_strategy-wf/est_features.parquet",
        wgs="../output/library_strategy-wf/wgs_features.parquet",
        chip="../output/library_strategy-wf/chip_features.parquet",
    script: "scripts/split_features.py"


rule outlier_detection:
    input: **rules.split_features.output
    output: 
        iso="../output/library_strategy-wf/isolation_forest_model.pkl",
        train="../output/library_strategy-wf/isolation_forest_training_data.pkl",
        test="../output/library_strategy-wf/isolation_forest_testing_data.pkl",
        outliers="../output/library_strategy-wf/rnaseq_outliers.parquet",
        rnaseq_inliers="../output/library_strategy-wf/rnaseq_inliers.pkl",
        mixin="../output/library_strategy-wf/contamination_mixin.parquet",
    script: "scripts/outlier_detection.py"


rule explain_outliers:
    input: 
        iso=rules.outlier_detection.output.iso,
        features=rules.outlier_detection.output.test,
        outliers=rules.outlier_detection.output.outliers,
    output:
        shap_model="../output/library_strategy-wf/shap_model.pkl",
        shap_values="../output/library_strategy-wf/shap_values.pkl",
    script: "scripts/explain_outliers.py"


################################################################################
# Plots
################################################################################
rule library_strategy_umap:
    input: 
        umap=rules.umap_prealn_features.output[0],
        labels=rules.pull_sra_strategy_and_selection_from_mongo.output.data,
    output: "../output/library_strategy-wf/library_strategy_umap.svg"
    script: "scripts/library_strategy_umap.py"


rule rnaseq_outlier_umap:
    input: 
        umap=rules.umap_prealn_features.output[0],
        labels=rules.pull_sra_strategy_and_selection_from_mongo.output.data,
        outliers=rules.outlier_detection.output.outliers,
    output: "../output/library_strategy-wf/rnaseq_outlier_umap.svg"
    script: "scripts/rnaseq_outlier_umap.py"


rule outlier_detection_contamination:
    input: rules.outlier_detection.output.mixin
    output: "../output/library_strategy-wf/outlier_detection_contamination.svg"
    script: "scripts/outlier_detection_contamination.py"


rule outlier_detection_feature_importance:
    input: 
        features=rules.outlier_detection.output.test,
        shap_model=rules.explain_outliers.output.shap_model,
        shap_values=rules.explain_outliers.output.shap_values,
    output: "../output/library_strategy-wf/outlier_detection_feature_importance.svg"
    script: "scripts/outlier_detection_feature_importance.py"

################################################################################
# Refine library strategy and selection metadata using free text
################################################################################
rule free_text_library_strategy:
    output: "../output/library_strategy-wf/free_text_library_strategy.parquet"
    script: "scripts/free_text_library_strategy.py"

# TODO: Re-think aggregation because I removed RF.
rule metadata_integration:
    input:
        sra=rules.pull_sra_strategy_and_selection_from_mongo.output.data,
        free_text=rules.free_text_library_strategy.output[0],
        # forest=rules.random_forest.output["predicted_labels"],
    output: "../output/library_strategy-wf/summarized_metadata.parquet"
    script: "scripts/metadata_integration.py"
