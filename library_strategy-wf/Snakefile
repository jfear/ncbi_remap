""" Library Strategy processing workflow

`Library Strategy` is the description of what kind of sequencing was done
(i.e., RNA-Seq, ChIP-Seq, WGS). The SRA uses higher level terms that may not
be the most accurate description of a sample. I would like to provide more
detailed descriptions of the sample if they are availabled in the free text
metadata. I also want to validate the high level metadata, to ensure that
submissions with a mixture of strategies (e.g., RNA-Seq and ChIP-Seq) did not
swap any sample labels.

I will focus on RNA-Seq samples for downstream analysis, so it is most
important that these samples are correctly identified. I will use a data
driven machine learning approach. I will use various features from the
`prealn-wf` with a cross validation approach to confidently identify samples
that behave as expected.

"""
singularity: "../singularity/drosSRA_workflow.sif"

rule targets:
    input:
        "../output/library_strategy-wf/sra_strategy_selection.parquet",
        "../output/library_strategy-wf/pca_prealn_features_embeddings.parquet",
        "../output/library_strategy-wf/umap_prealn_features.parquet",
        "../output/library_strategy-wf/rnaseq_inliers.pkl",
        "../output/library_strategy-wf/pairlot_rnaseq_features.tgz",
        "../output/library_strategy-wf/umap_library_strategy.svg",
        "../output/library_strategy-wf/umap_rnaseq_outliers.svg",
        "../output/library_strategy-wf/umap_est_outliers.svg",
        "../output/library_strategy-wf/umap_wgs_outliers.svg",
        "../output/library_strategy-wf/umap_chip_outliers.svg",
        "../output/library_strategy-wf/mixin_outliers.svg",
        "../output/library_strategy-wf/feature_importance.svg"


rule pull_sra_strategy_and_selection_from_mongo:
    output:
        data="../output/library_strategy-wf/sra_strategy_selection.parquet",
        summary="../output/library_strategy-wf/sra_strategy_selection_summary_table.tsv",
    script: "scripts/pull_sra_strategy_and_selection_from_mongo.py"


################################################################################
# Prep Features
################################################################################
rule build_prealn_features:
    input: "../output/srx2srr.csv"
    params:
        libsize="../output/fastq-wf/libsize",
        fastq_screen="../output/prealn-wf/fastq_screen",
        atropos="../output/prealn-wf/atropos",
        hisat2="../output/prealn-wf/hisat2",
        aln_stats="../output/prealn-wf/aln_stats",
        rnaseqmetrics="../output/prealn-wf/rnaseqmetrics",
        genebody_coverage="../output/prealn-wf/genebody_coverage",
        markduplicates="../output/prealn-wf/markduplicates",
        count_summary="../output/prealn-wf/count_summary",
        done="../output/prealn-wf/done"
    threads: 8
    output: "../output/library_strategy-wf/prealn_features.parquet",
    script: "scripts/build_prealn_features.py"


rule scale_prealn_features:
    input: rules.build_prealn_features.output[0]
    output: "../output/library_strategy-wf/scaled_prealn_features.parquet"
    script: "scripts/scale_prealn_features.py"


rule pca_prealn_features:
    input: rules.scale_prealn_features.output[0]
    output: 
        embeddings="../output/library_strategy-wf/pca_prealn_features_embeddings.parquet",
        loadings="../output/library_strategy-wf/pca_prealn_features_loadings.parquet",
        variance="../output/library_strategy-wf/pca_prealn_features_explained_variance.parquet",
    script: "scripts/pca_prealn_features.py"


rule umap_prealn_features:
    input: rules.scale_prealn_features.output[0]
    output: "../output/library_strategy-wf/umap_prealn_features.parquet",
    script: "scripts/umap_prealn_features.py"


################################################################################
# Find RNA-Seq Outliers
################################################################################
rule split_features:
    input:
        features=rules.build_prealn_features.output[0],
        labels=rules.pull_sra_strategy_and_selection_from_mongo.output.data
    output: 
        rnaseq="../output/library_strategy-wf/rnaseq_features.parquet",
        est="../output/library_strategy-wf/est_features.parquet",
        wgs="../output/library_strategy-wf/wgs_features.parquet",
        chip="../output/library_strategy-wf/chip_features.parquet",
    script: "scripts/split_features.py"


rule outlier_detection:
    input: **rules.split_features.output
    output: 
        iso="../output/library_strategy-wf/isolation_forest.pkl",
        rnaseq_inliers="../output/library_strategy-wf/rnaseq_inliers.pkl",
        mixin="../output/library_strategy-wf/contamination_mixin.parquet",
    script: "scripts/outlier_detection.py"

################################################################################
# Plots
################################################################################
rule plot_pairplot_rnaseq_features:
    input: rules.outlier_detection.output.iso
    output: "../output/library_strategy-wf/pairlot_rnaseq_features.tgz",
    script: "scripts/plot_pairplot_rnaseq_features.py"


rule plot_umap_library_strategy:
    input: 
        umap=rules.umap_prealn_features.output[0],
        labels=rules.pull_sra_strategy_and_selection_from_mongo.output.data,
    output: "../output/library_strategy-wf/umap_library_strategy.svg"
    script: "scripts/plot_umap_library_strategy.py"


rule plot_umap_rnaseq_outliers:
    input: 
        umap=rules.umap_prealn_features.output[0],
        labels=rules.pull_sra_strategy_and_selection_from_mongo.output.data,
        iso=rules.outlier_detection.output.iso
    output: "../output/library_strategy-wf/umap_rnaseq_outliers.svg"
    script: "scripts/plot_umap_rnaseq_outliers.py"

rule plot_umap_other_outliers:
    input:
        umap=rules.umap_prealn_features.output[0],
        iso=rules.outlier_detection.output.iso,
        est=rules.split_features.output.est,
        wgs=rules.split_features.output.wgs,
        chip=rules.split_features.output.chip,
    output:
        est="../output/library_strategy-wf/umap_est_outliers.svg",
        wgs="../output/library_strategy-wf/umap_wgs_outliers.svg",
        chip="../output/library_strategy-wf/umap_chip_outliers.svg",
    script: "scripts/plot_umap_other_outliers.py"

rule plot_mixin_outliers:
    input: rules.outlier_detection.output.mixin
    output: "../output/library_strategy-wf/mixin_outliers.svg"
    script: "scripts/plot_mixin_outliers.py"


rule plot_feature_importance:
    input: rules.outlier_detection.output.iso
    output: "../output/library_strategy-wf/feature_importance.svg"
    script: "scripts/plot_feature_importance.py"

################################################################################
# Refine library strategy and selection metadata using free text
################################################################################
rule free_text_library_strategy:
    output: "../output/library_strategy-wf/free_text_library_strategy.parquet"
    script: "scripts/free_text_library_strategy.py"

# TODO: Re-think aggregation because I removed RF.
rule metadata_integration:
    input:
        sra=rules.pull_sra_strategy_and_selection_from_mongo.output.data,
        free_text=rules.free_text_library_strategy.output[0],
        # forest=rules.random_forest.output["predicted_labels"],
    output: "../output/library_strategy-wf/summarized_metadata.parquet"
    script: "scripts/metadata_integration.py"
