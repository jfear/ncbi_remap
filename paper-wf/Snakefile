"""Workflow for building parts of the paper."""

configfile: "../config/common.yaml"
ASSEMBLY = config["assembly"]
TAG = config["tag"]

rule all:
    input:
        "../output/paper-wf/gene_metadata.tsv",
        "../output/paper-wf/srx_prealn.tsv",
        "../output/paper-wf/srx_aln.tsv",
        # Figures
        "../output/paper-wf/figures/sample_submission.svg",
        "../output/paper-wf/figures/layout.svg",
        "../output/paper-wf/figures/read_length.svg",
        "../output/paper-wf/figures/library_size.svg",
        "../output/paper-wf/figures/prealn_features.svg",
        "../output/paper-wf/figures/feature_importance.svg",


################################################################################
# Data Wrangling
################################################################################
rule gene_metadata:
    input: f"../lcdb-references/{ASSEMBLY}/{TAG}/gtf/{ASSEMBLY}_{TAG}.gtf"
    output: "../output/paper-wf/gene_metadata.tsv"
    script: "scripts/gene_metadata.py"


rule srx_metadata:
    input: 
        # sramongo
        "../output/library_strategy-wf/summarized_metadata.parquet"
    output: "../output/paper-wf/srx_metadata.tsv"
    script: "scripts/srx_metadata.py"


rule srr_prealn:
    input: "../output/sra.h5"
    output: "../output/paper-wf/srr_prealn.tsv"
    script: "scripts/srr_prealn.py"


rule srx_prealn:
    """Simple aggregation of prealn to srx level."""
    input: 
        srr=rules.srr_prealn.output[0],
        srx_metadata=rules.srx_metadata.output[0]
    output: "../output/paper-wf/srx_prealn.tsv"
    script: "scripts/srx_prealn.py"

rule srx_aln:
    input:
        store="../output/sra.h5",
        library_strategy="../output/library_strategy-wf/summarized_metadata.parquet",
    params: 
        genic_pattern=lambda wildcards: "../output/aln-wf/samples/{srx}/{srx}.bam.counts"
    output: "../output/paper-wf/srx_aln.tsv"
    script: "scripts/srx_aln.py"

################################################################################
# Pre-alignment plots
################################################################################
rule sample_submission:
    input: rules.srx_prealn.output[0]
    output: "../output/paper-wf/figures/sample_submission.svg"
    params:
        style=("sra", "sra_talk"),
        title="Sample Submission",
        bar_plot_kwargs=dict(color="C0"),
        reg_plot_kwargs=dict(lowess=True, color="C1"),
    script: "scripts/plot_sample_submission.py"

rule layout:
    input: rules.srx_prealn.output[0]
    output: "../output/paper-wf/figures/layout.svg"
    params:
        style=("sra", "sra_talk"),
        title="Library Layout",
        plot_kwargs=dict(color="lightgray", edgecolor="k"),
        ax_kwargs=dict(ylabel="Samples")
    script: "scripts/plot_layout.py"

rule read_length:
    input: rules.srx_prealn.output[0]
    output: "../output/paper-wf/figures/read_length.svg"
    params:
        style=("sra", "sra_talk"),
        title="Read Length",
        plot_kwargs=dict(
            showfliers=False,
            color="lightgray",
            boxprops=dict(edgecolor="k"),
            medianprops=dict(color="k"),
            whiskerprops=dict(color="k"),
            capprops=dict(color="k"),
        ),
        ax_kwargs=dict(ylabel="Avg. Read Length (bp)", xlabel="Year"),
    script: "scripts/plot_read_length.py"


rule library_size:
    input: rules.srx_prealn.output[0]
    output: "../output/paper-wf/figures/library_size.svg"
    params:
        style=("sra", "sra_talk"),
        title="Libary Size",
        plot_kwargs=dict(),
        joint_ax_kwargs=dict(),
        libsize_ax_kwargs=dict(),
        duplication_ax_kwargs=dict(),
    script: "scripts/plot_libsize.py"

rule prealn_features:
    input: rules.srx_prealn.output[0]
    output: "../output/paper-wf/figures/prealn_features.svg"
    params:
        style=("sra", "sra_talk"),
    script: "scripts/plot_prealn_features.py"

rule feature_importance:
    input: "../output/library_strategy-wf/random_forest_feature_importance.tsv"
    output: "../output/paper-wf/figures/feature_importance.svg"
    params:
        style=("sra", "sra_talk"),
    script: "scripts/plot_feature_importance.py"

################################################################################
# Alignment plots
################################################################################

